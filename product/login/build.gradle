apply from: "$rootDir/gradle/spring-boot-app.gradle"
apply from: "$rootDir/gradle/spring-boot-jdbc.gradle"
apply from: "$rootDir/gradle/spring-boot-web.gradle"
apply from: "$projectDir/src/heroku/build.gradle"

description = 'The Codekvast Login server'
applicationName = 'codekvast-login'
archivesBaseName = 'codekvast-login'
version = codekvastVersion
mainClassName = 'io.codekvast.login.CodekvastLoginApplication'

dependencies {
    compile project(":product:common")
    compile 'org.springframework.security:spring-security-config'
    compile 'org.springframework.security:spring-security-core'
    compile 'org.springframework.security:spring-security-web'
    compile 'org.springframework.security:spring-security-oauth2-core'
    compile 'org.springframework.security:spring-security-oauth2-client'
    compile 'org.springframework.security:spring-security-oauth2-jose'
    compile "com.google.code.gson:gson:$gsonVersion"
    compile 'com.jayway.jsonpath:json-path:2.4.0'

    runtime project(':product:db-migration')
    runtime 'org.codehaus.groovy:groovy-templates:2.5.3'
    runtime 'org.codehaus.groovy:groovy:2.4.15'

    testCompile "com.github.tomakehurst:wiremock:$wiremockVersion"
}

run {
    dependsOn startMariadb

    jvmArgs = [
        '-ea',
    ]

    args = [
        "--logging.file=$buildDir/log/${applicationName}.log",
        "--spring.profiles.active=dev-secrets,webpack-dev-server"
    ]
}

bootRun {
    dependsOn startMariadb

    jvmArgs = run.jvmArgs
    args = run.args
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
bootJar {
    launchScript {
        properties([
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Login',
            'initInfoDescription'     : 'Codekvast Login handles logins',
        ])
    }
}
