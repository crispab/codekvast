apply from: "$rootDir/gradle/spring-boot-app.gradle"
apply from: "$rootDir/gradle/spring-boot-jdbc.gradle"
apply from: "$rootDir/gradle/spring-boot-web.gradle"
apply from: "$projectDir/src/heroku/build.gradle"

description = 'The Codekvast Login server'
applicationName = 'codekvast-login'
archivesBaseName = 'codekvast-login'
version = codekvastVersion
mainClassName = 'io.codekvast.login.CodekvastLoginApplication'

dependencies {
    implementation project(":product:server:common")
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework.security:spring-security-web'
    implementation 'org.springframework.security:spring-security-oauth2-core'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'com.jayway.jsonpath:json-path:2.4.0'

    runtimeOnly project(':product:server:db-migration')

    def groovyVersion = '2.4.15' // prevent a bug in groovy-templates 2.5.4
    runtimeOnly "org.codehaus.groovy:groovy-templates:$groovyVersion"
    runtimeOnly "org.codehaus.groovy:groovy:$groovyVersion"
    runtimeOnly "org.codehaus.groovy:groovy-xml:$groovyVersion"

    testImplementation "com.github.tomakehurst:wiremock:$wiremockVersion"
}

run {
    dependsOn startMariadb

    jvmArgs = [
        '-enableassertions',
    ]

    args = [
        "--logging.file=$buildDir/log/${applicationName}.log",
        "--spring.profiles.active=dev-secrets,webpack-dev-server"
    ]
}

bootRun {
    dependsOn startMariadb

    jvmArgs = run.jvmArgs
    args = run.args
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
bootJar {
    launchScript {
        properties([
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Login',
            'initInfoDescription'     : 'Codekvast Login handles logins',
        ])
    }
}
