import io.codekvast.backoffice.facts.CollectionStarted
import io.codekvast.backoffice.facts.ContactDetails
import io.codekvast.backoffice.service.MailSender
import io.codekvast.common.messaging.model.CodeBaseReceivedEvent
import io.codekvast.common.messaging.model.CodekvastEvent

global java.time.Clock clock
global Long customerId
global io.codekvast.backoffice.service.MailSender mailSender
global org.slf4j.Logger logger

rule "Register that a codebase has been received"
when
  event: CodeBaseReceivedEvent()
  not(CollectionStarted())
then
   insert(CollectionStarted.of(event));
end

rule "Send welcome mail unless done before"
when
  $cs: CollectionStarted(welcomeMailSentTo == null)
  $cd: ContactDetails()
then
  mailSender.sendMail(MailSender.Template.WELCOME_TO_CODEKVAST, $cd.getContactEmail(), customerId);
  modify($cs) {
    setWelcomeMailSentTo($cd.getContactEmail()),
    setWelcomeMailSentAt(clock.instant())
  }
end
