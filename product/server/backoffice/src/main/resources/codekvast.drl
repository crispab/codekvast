import io.codekvast.backoffice.facts.CollectionStarted
import io.codekvast.backoffice.facts.UserAuthenticated
import io.codekvast.common.messaging.model.CodekvastEvent
import io.codekvast.common.messaging.model.CollectionStartedEvent
import io.codekvast.common.messaging.model.UserAuthenticatedEvent

global java.time.Clock clock
global Long customerId
global io.codekvast.backoffice.service.MailSender mailSender

rule "User has authenticated for the first time"
when
  $uae: UserAuthenticatedEvent()
  not(UserAuthenticated(emailAddress == $uae.emailAddress, authenticationProvider == $uae.authenticationProvider))
then
   insert(UserAuthenticated.of($uae));
end

rule "User has authenticated again"
when
  $uae: UserAuthenticatedEvent()
  $uaf: UserAuthenticated(emailAddress == $uae.emailAddress, authenticationProvider == $uae.authenticationProvider)
then
   modify($uaf) {
      setCount($uaf.getCount() + 1),
      setInstant(clock.instant())
   }
end

rule "Detect that collection has started"
when
  $cse: CollectionStartedEvent()
  not(CollectionStarted())
then
   insert(CollectionStarted.of($cse));
end

rule "Send welcome mail when collection has started"
when
  $cs: CollectionStarted(welcomeMailSent == false)
then
  mailSender.sendMail("welcome-collection-has-started", customerId);
  modify($cs) {
    setWelcomeMailSent(true)
  }
end

rule "Retract transient event facts"
salience -1000
when
  $ce: CodekvastEvent()
then
   retract($ce)
end
