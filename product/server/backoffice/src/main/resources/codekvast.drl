import io.codekvast.backoffice.facts.CollectionStarted
import io.codekvast.backoffice.facts.ContactDetails
import io.codekvast.common.messaging.model.CodekvastEvent
import io.codekvast.common.messaging.model.CollectionStartedEvent

global java.time.Clock clock
global Long customerId
global io.codekvast.backoffice.service.MailSender mailSender

rule "Collection has started"
when
  $cse: CollectionStartedEvent()
  not(CollectionStarted())
then
   insert(CollectionStarted.of($cse));
end

rule "Send welcome mail when collection has started"
when
  $cs: CollectionStarted(welcomeMailSent == false)
  $cd: ContactDetails()
then
  mailSender.sendMail("welcome-collection-has-started", customerId, $cd.getContactEmail());
  modify($cs) {
    setWelcomeMailSent(true)
  }
end

rule "Retract event facts"
salience -1000
when
  $ce: CodekvastEvent()
then
   retract($ce)
end
