apply from: "$rootDir/gradle/spring-boot-app.gradle"
apply from: "$rootDir/gradle/spring-boot-jdbc.gradle"
apply from: "$rootDir/gradle/spring-boot-web.gradle"
apply from: "$rootDir/gradle/kotlin-jdk8.gradle"
apply from: "$rootDir/gradle/google-jib.gradle"

description = 'The Codekvast Backoffice'
applicationName = 'codekvast-backoffice'
archivesBaseName = 'codekvast-backoffice'
version = codekvastVersion
mainClassName = 'io.codekvast.backoffice.CodekvastBackofficeApplicationKt'

apply from: "$projectDir/src/integrationTest/build.gradle"

configurations {
    codekvastagent
}

dependencies {
    implementation project(':product:server:common')
    implementation "org.drools:drools-compiler:$droolsVersion"
    implementation "org.drools:drools-mvel:$droolsVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'org.springframework.boot:spring-boot-starter-mustache'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    runtimeOnly project(':product:server:db-migration')

    codekvastagent project(path: ":product:agent:java-agent", configuration: 'shadow')
}

run {
    dependsOn startDockerContainers
    jvmArgs = ['-enableassertions',]
    args = [
            "--logging.file.name=$buildDir/${applicationName}.log",
            '--spring.profiles.active=dev'
    ]
}

bootRun {
    dependsOn startDockerContainers
    jvmArgs = run.jvmArgs
    args = run.args
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
bootJar {
    launchScript {
        properties([
                'initInfoProvides'        : applicationName,
                'initInfoShortDescription': 'Codekvast Backoffice',
                'initInfoDescription'     : 'Codekvast Backoffice offers a management interface for Codekvast',
        ])
    }
}

task generateCodekvastConfig {
    group "Build"
    description "Generates a codekvast.conf for test"

    def configFile = file("$buildDir/codekvast.conf")

    inputs.files file("build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Codekvast against Sample App
appName = $archivesBaseName
appVersion = $codekvastDisplayVersion
# aspectjOptions = -verbose -showWeaveInfo
codeBase = build/libs/$archivesBaseName-$version-all.jar
methodVisibility = all
packages = io.codekvast
excludePackages = io.codekvast.common.aspects
bridgeAspectjMessagesToJUL = false
environment = Gradle

# serverUrl = https://api-staging.codekvast.io
httpProxyHost =   
httpProxyPort = 4711
"""
    }
}

processResources.dependsOn generateCodekvastConfig

task launchBackofficeWithCodekvastAgentAttached(type: Exec, dependsOn: [bootJar, generateCodekvastConfig, ":product:agent:java-agent:shadowJar", startDockerContainers]) {
    def javaVersion = sdkmanJavaVersion_11

    description "Launches the executable jar produced by bootJar with OpenJDK " + javaVersion
    group "Application"

    executable "${System.getenv('HOME')}/.sdkman/candidates/java/${javaVersion}/bin/java"
    args "-XX:+ExitOnOutOfMemoryError"
    args "-enableassertions"
    args "-Xms10m"
    args "-Xmx100m"
    // args "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5011"
    args "-javaagent:${configurations.codekvastagent.asPath}"
    args "-Dcodekvast.configuration=$generateCodekvastConfig.outputs.files.asPath"

    args "-jar", "build/libs/${archivesBaseName}-${version}-all.jar"
    args "--logging.level.root=INFO"
    args "--logging.level.io.codekvast=INFO"
    args "--logging.file.name=$buildDir/${applicationName}.log"

    args "--spring.profiles.active=dev,stress-test"
}
