//--- Frontend -----------------------------------------------------------------------------------

task frontendInstall(type: Exec) {
    description 'Installs the node modules and tools required for frontend development'
    group 'Frontend Development'

    workingDir file('src/webapp2')
    executable 'yarn'
    args 'install'

    inputs.files file('src/webapp2/package.json')
    inputs.dir file('src/webapp2/node_modules')
    outputs.file file('src/webapp2/yarn.lock')
}

task frontendTest(type: Exec) {
    description "Runs Typescript unit tests with Karma and Jasmine"
    group "Frontend Development"

    dependsOn frontendInstall
    mustRunAfter test

    def timestampFile = file("$buildDir/${name}.timestamp")

    inputs.files file('src/webapp2/angular.json')
    inputs.files file('src/webapp2/package.json')
    inputs.files file('src/webapp2/tsconfig.json')
    inputs.dir file('src/webapp2/src')
    outputs.file timestampFile

    workingDir file('src/webapp2')
    executable 'npm'
    args 'run', 'test'

    doLast {
        timestampFile.parentFile.mkdirs()
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn frontendTest

task frontendLint(type: Exec) {
    description "Runs tslint on all TypeScript sources"
    group "Frontend Development"

    dependsOn frontendInstall
    mustRunAfter test

    def timestampFile = file("$buildDir/${name}.timestamp")

    inputs.files file('src/webapp2/angular.json')
    inputs.files file('src/webapp2/package.json')
    inputs.files file('src/webapp2/tsconfig.json')
    inputs.dir file('src/webapp2/src')
    inputs.property "version", codekvastDisplayVersion
    outputs.file timestampFile

    workingDir file('src/webapp2')
    executable 'npm'
    args 'run', 'lint'

    doLast {
        timestampFile.parentFile.mkdirs()
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn frontendLint

task frontendStart(type: Exec) {
    description 'Starts the webpack dev server on port 8089'
    group 'Frontend Development'

    dependsOn frontendInstall

    workingDir file('src/webapp2')
    executable 'npm'
    args 'start'
}

task frontendWebpack(type: Exec) {
    description "Builds the frontend webpack bundles for production"
    group "Frontend Development"
    dependsOn frontendTest

    inputs.files file('src/webapp2/angular.json')
    inputs.files file('src/webapp2/package.json')
    inputs.files file('src/webapp2/tsconfig.json')
    inputs.dir file('src/webapp2/src')
    inputs.property "version", codekvastDisplayVersion

    outputs.dir file('src/webapp2/dist')

    workingDir file('src/webapp2')
    executable 'npm'
    args 'run', 'build'
    environment['CODEKVAST_VERSION'] = codekvastDisplayVersion
}

task frontendClean(type: Exec) {
    description "Cleans the frontend webpack bundles"
    group "Frontend Development"

    workingDir file('src/webapp2')
    executable 'npm'
    args 'run', 'clean'
}

clean.dependsOn frontendClean
