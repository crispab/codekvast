buildscript { dependencies { classpath "org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE" } }
apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'application'

description = "The Duck agent"
archivesBaseName = "duck-agent"
version = project.parent.parent.version
sourceCompatibility = 1.7
mainClassName = "se.crisp.duck.agent.main.DuckAgentMain"

dependencies {
    compile project(":product:agent:util")
    compile project(":product:server:agent-delegate")
    compile "org.springframework.boot:spring-boot-starter"
    compile libs['logback']
    compile libs['lombok']
    compile libs['javax-inject']
    compile libs['aspectjweaver']
    compile libs['reflections']
    compile libs['slf4j-api']

    testCompile testLibs
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

jar {
    exclude "lombok"
}

test.dependsOn ":sample:standalone-app:jar"

// We sensor the sample standalone-app by default...
run {
    dependsOn = [":sample:standalone-app:installApp", ":sample:standalone-app:generateDuckConfig"]
    args tasks.getByPath(":sample:standalone-app:generateDuckConfig").configFile
    // args "/usr/local/src/ws/tnm/tnm_mainserver_main/duck.properties"
}

jar.dependsOn tasks.getByPath(":product:agent:util:test")

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "logback.xml"
    rename { "${it}.sample" }
}

applicationDistribution.from(tasks.getByPath(":product:agent:util:test").sampleConfigFile) {
    into "conf"
}

startScripts {
    doLast {
        outputs.files.each { script ->
            logger.info "Adjusting CLASSPATH in {} to start with \$APP_HOME/conf/ ...", script.name
            def text = script.text
            text = text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
            script.text = text
        }
    }
}
