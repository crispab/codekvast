plugins {
    id 'java'
    id 'com.ofg.uptodate' version "1.1.0"
}

description = "Code that is shared between codekvast-agent and the collector"
archivesBaseName = "codekvast-agent-util"
version = project.parent.parent.version
sourceCompatibility = 1.6

dependencies {
    compile lombok
    compile "org.aspectj:aspectjweaver:$aspectjVersion"

    // Absolutely NO other compile or runtime dependencies are allowed here!

    // All transitive compile and runtime dependencies will end up in the collected app's system
    // class path. (Lombok will be excluded when assembling the codekvast-collector jar).

    testCompile junit
    testCompile mockito
}

test {
    // Make it possible to retrieve the sample config files produced by a test
    ext.sampleAgentConfigFile = file("$buildDir/codekvast-agent.conf.sample")
    ext.sampleCollectorConfigFile = file("$buildDir/codekvast-collector.conf.sample")

    // Also make the file name available to the test that produces it...
    systemProperty 'sampleAgentConfigFile.path', sampleAgentConfigFile.absolutePath
    systemProperty 'sampleCollectorConfigFile.path', sampleCollectorConfigFile.absolutePath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

