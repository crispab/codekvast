import org.apache.tools.ant.filters.ReplaceTokens

buildscript { dependencies { classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion" } }
plugins {
    id 'java'
    id 'application'
    id 'com.ofg.uptodate' version "1.1.0"
}
apply plugin: 'spring-boot'

description = "The Codekvast agent"
archivesBaseName = "codekvast-agent"
version = project.parent.parent.version
sourceCompatibility = 1.7
targetCompatibility = 1.7
mainClassName = "se.crisp.codekvast.agent.main.CodekvastAgentMain"

dependencies {
    compile "ch.qos.logback:logback-classic"
    compile "org.reflections:reflections:0.9.9"
    compile "org.springframework.boot:spring-boot-starter"
    compile lombok
    compile project(":product:agent:util")
    compile project(":product:server:agent-delegate")

    testCompile junit
    testCompile mockito
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/application.properties'
        filter(ReplaceTokens, tokens: gitVersionTokens + ['gradle.name'       : project.name,
                                                          'gradle.description': project.description,
                                                          'gradle.version'    : project.version]
        )
    }
}

jar {
    exclude "lombok"
}

test.dependsOn ":sample:standalone-app:jar"

// We collector the sample standalone-app by default...
run {
    dependsOn = [":sample:standalone-app:installApp", ":sample:standalone-app:generateCodekvastConfig"]
    args tasks.getByPath(":sample:standalone-app:generateCodekvastConfig").configFile
    // args "/home/qolha/work/git/tnm/codekvast.conf"
}

jar.dependsOn tasks.getByPath(":product:agent:util:test")

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "logback.xml"
    rename { "${it}.sample" }
}

applicationDistribution.from(tasks.getByPath(":product:agent:util:test").sampleCollectorConfigFile) {
    into "conf"
}

applicationDistribution.from(tasks.getByPath(":product:agent:util:test").sampleAgentConfigFile) {
    into "conf"
}

startScripts {
    doLast {
        outputs.files.each { script ->
            logger.info "Adjusting CLASSPATH in {} to start with \$APP_HOME/conf/ ...", script.name
            script.text = script.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
        }
    }
}
