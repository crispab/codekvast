buildscript {
    repositories { maven { url 'https://maven.eveoh.nl/content/repositories/releases' } }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        classpath 'nl.eveoh:gradle-aspectj:1.4'
    }
}

apply plugin: 'aspectj'
apply plugin: 'shadow'

description = "The javaagent part of Codekvast agent"
archivesBaseName = "codekvast-agent-collector"
version = project.parent.parent.version
sourceCompatibility = 1.6

dependencies {
    // cannot use lombok here, since this is an AspectJ project
    compile project(":product:agent:util"), { transitive = false }
    compile "org.aspectj:aspectjweaver:$aspectjVersion"

    // Absolutely NO other compile or runtime dependencies are allowed here!
    // All (transitive) compile and runtime dependencies will end up in the instrumented app's system
    // class path, which is unacceptable.
}

jar { manifest { attributes("Premain-Class": "se.crisp.codekvast.agent.collector.CodekvastCollector") } }
shadow { outputFile = file("$buildDir/distributions/codekvast-collector-${version}.jar") }

// Make a shadow jar consisting of codekvast-agent-collector and it's dependencies (i.e. aspectjweaver-n.n.jar)
assemble.dependsOn shadowJar
