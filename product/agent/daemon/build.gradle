apply from: "$rootDir/gradle/java-8.gradle"
apply from: "$rootDir/gradle/sources-javadoc.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply plugin: 'application'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/standalone-h2server.gradle"

description = "The Codekvast daemon"
applicationName = "codekvast-daemon"
archivesBaseName = "codekvast-daemon"
version = project.parent.parent.version
mainClassName = "se.crisp.codekvast.agent.daemon.CodekvastDaemon"

import org.apache.tools.ant.filters.ReplaceTokens

configurations {
    springLoaded
}

dependencies {
    compile lombok
    compile project(':product:agent:agent-lib')
    compile 'javax.inject:javax.inject:1'
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile 'org.reflections:reflections:0.9.10'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.flywaydb:flyway-core'
    compile "com.opencsv:opencsv:$opencsvVersion"
    compile 'com.hierynomus:sshj:0.15.0'

    runtime h2database
    runtime 'ch.qos.logback:logback-classic'

    testCompile junit
    testCompile mockito
    testCompile "org.springframework.boot:spring-boot-starter-test"

    springLoaded springLoadedAgent
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/application.properties'
        filter(ReplaceTokens, tokens: gitVersionTokens + ['gradle.name'          : archivesBaseName,
                                                          'gradle.description'   : project.description,
                                                          'gradle.version'       : project.version,
                                                          'gradle.displayVersion': codekvastDisplayVersion]
        )
    }
}

flyway {
    url = "jdbc:h2:tcp://localhost/$buildDir/database/codekvast-daemon"
    user = 'codekvast'
    password = 'codekvast'
    locations = ['classpath:/database/migration', 'classpath:/se/crisp/codekvast/agent/daemon/database/migration']
}

jar {
    exclude "lombok"
}

run {
    jvmArgs = ['-ea']

    args = [
            "--logging.file=$buildDir/log/codekvast-daemon.log",
            '--logging.level.se.crisp.codekvast=DEBUG',
            '--h2database.options=TRACE_LEVEL_FILE=4;AUTO_SERVER=FALSE;DB_CLOSE_ON_EXIT=FALSE',
            '--codekvast.dataProcessingIntervalSeconds=60',
            "--codekvast.databasePath=tcp://localhost/$buildDir/database",
            '--codekvast.exportFile=/tmp/codekvast/.export/codekvast-data-#hostname#.zip',
            '--codekvast.environment=Gradle',
            // During test
            //"--codekvast.exportFile=/home/qolha/Dropbox/Kund/Transmode/codekvast/codekvast-data-#hostname#.zip",
            // "--codekvast.uploadToHost=fjottas.local",
            // "--codekvast.uploadToPath=/tmp/codekvast/.export",
    ]
}
run.dependsOn startDatabase

bootRun {
    jvmArgs = [
            "-javaagent:${configurations.springLoaded.asPath}",
            '-noverify',
    ] + run.jvmArgs
    args = run.args
}
bootRun.dependsOn startDatabase

jar.dependsOn tasks.getByPath(":product:agent:agent-lib:test")

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "default.properties"
    rename { "codekvast-daemon.properties.sample.${version}" }
}

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "codekvast-collector.conf.sample", "byte-code-added-methods.txt", "byte-code-enhanced-methods.txt"
    rename { "${it}.${version}" }
}

applicationDistribution.from("src/dist") {
    into "/"
    filter { it.replace("@CODEKVAST_VERSION@", project.version).replace("@ASPECTJ_VERSION@", aspectjVersion) }
}

applicationDefaultJvmArgs = ["-Dlogging.file=/var/log/${applicationName}.log"]

startScripts {
    doLast {
        windowsScript.text = windowsScript.text
                .replace('CLASSPATH=%APP_HOME%\\lib', 'CLASSPATH=%APP_HOME%\\conf\\;%APP_HOME%\\lib')

        unixScript.text = unixScript.text
                .replace('APP_HOME=', 'export APP_HOME=')
                .replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
    }
}
