apply from: "$rootDir/gradle/spring-boot-app.gradle"
apply from: "$rootDir/gradle/spring-boot-jdbc.gradle"
apply from: "$rootDir/gradle/spring-boot-web.gradle"
apply from: "$rootDir/gradle/kotlin-jdk8.gradle"
apply plugin: 'groovy'

description = 'The Codekvast dashboard'
applicationName = 'codekvast-dashboard'
archivesBaseName = 'codekvast-dashboard'
version = codekvastVersion
group = 'crisp'
mainClassName = 'io.codekvast.dashboard.CodekvastDashboardApplicationKt'

apply from: "$projectDir/src/webapp/build.gradle"
apply from: "$projectDir/src/integrationTest/build.gradle"
apply from: "$projectDir/src/systemTest/build.gradle"

dependencies {
    compile project(':product:agent-model')
    compile project(':product:common')
    compile "com.google.code.gson:gson:$gsonVersion"
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'io.jsonwebtoken:jjwt:0.9.0'

    runtime project(':product:db-migration')
}

bootJar {
    dependsOn frontendWebpack

    // Spring Boot automatically serves static content from static/**

    // Output from frontendWebpack
    into('static') {
        from('src/webapp/dist') {
            exclude '**/*.css.map'
            exclude '**/*.js.map'
            exclude '**/test/**'
        }
    }

    launchScript {
        properties([
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Dashboard',
            'initInfoDescription'     : 'Codekvast Dashboard shows the collected data',
        ])
    }
}

run {
    group "Backend Development"
    // dependsOn startMariadb, ":product:dashboard:frontendWebpack", installDist, generateCodekvastConfig, ':product:java-agent:shadowJar'
    dependsOn startMariadb, ":product:dashboard:frontendWebpack"

    jvmArgs = [
        '-ea',
        // "-Dcodekvast.configuration=$generateCodekvastConfig.outputs.files.asPath",
    ]

    args = ["--logging.file=$buildDir/log/${applicationName}.log",
            '--codekvast.fileImportIntervalSeconds=10',
            '--codekvast.dashboardJwtExpirationHours=-5'
    ]
}

bootRun {
    group "Backend Development"
    dependsOn startMariadb

    jvmArgs = run.jvmArgs
    args = run.args
}
