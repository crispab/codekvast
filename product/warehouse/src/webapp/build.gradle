//--- Frontend -----------------------------------------------------------------------------------

task frontendInstall(type: Exec) {
    description 'Installs the node modules and tools required for frontend development'
    group 'Frontend Development'

    workingDir file('src/webapp')
    executable 'npm'
    args 'install'

    inputs.file file('src/webapp/package.json')
    inputs.file file('src/webapp/typings.json')
    outputs.dir file('src/webapp/node_modules')
    outputs.dir file('src/webapp/typings')
}

task frontendTest(type: Exec) {
    description "Runs Typescript unit tests with Karma and Jasmine"
    group "Frontend Development"

    dependsOn frontendInstall
    mustRunAfter test

    def timestampFile = file("$buildDir/frontendTest.timestamp")

    inputs.file 'src/webapp/package.json'
    inputs.file 'src/webapp/tsconfig.json'
    inputs.file 'src/webapp/webpack.config.json'
    inputs.files fileTree('src/webapp/config')
    inputs.files fileTree('src/webapp/src')
    outputs.file timestampFile

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'test'

    doLast {
        timestampFile.parentFile.mkdirs()
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn frontendTest

task frontendLint(type: Exec) {
    description "Runs tslint on all TypeScript sources"
    group "Frontend Development"

    dependsOn frontendInstall
    mustRunAfter test

    def timestampFile = file("$buildDir/frontendLint.timestamp")

    inputs.file 'src/webapp/package.json'
    inputs.file 'src/webapp/tsconfig.json'
    inputs.file 'src/webapp/tslint.json'
    inputs.files fileTree('src/webapp/config')
    inputs.files fileTree('src/webapp/src')
    inputs.property "version", codekvastDisplayVersion
    outputs.file timestampFile

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'lint'

    doLast {
        timestampFile.parentFile.mkdirs()
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn frontendLint

task frontendStart(type: Exec) {
    description 'Starts the webpack dev server on port 8088'
    group 'Frontend Development'

    dependsOn frontendInstall

    workingDir file('src/webapp')
    executable 'npm'
    args 'start'
}

task frontendBuild(type: Exec) {
    description "Builds the frontend webpack bundles for production"
    group "Frontend Development"
    dependsOn frontendTest

    inputs.file file('src/webapp/package.json')
    inputs.file file('src/webapp/tsconfig.json')
    inputs.file file('src/webapp/webpack.config.json')
    inputs.dir file('src/webapp/assets')
    inputs.dir file('src/webapp/config')
    inputs.dir file('src/webapp/src')
    inputs.property "version", codekvastDisplayVersion

    outputs.dir file('src/webapp/dist')

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'build'
    environment['CODEKVAST_VERSION']=codekvastDisplayVersion
}

task frontendClean(type: Exec) {
    description "Cleans the frontend webpack bundles"
    group "Frontend Development"

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'clean'
}

clean.dependsOn frontendClean

jar {
    dependsOn frontendBuild

    // Spring Boot automatically serves static content from static/**

    // Output from frontendBuild
    into('static') {
        from('src/webapp/dist') {
            exclude '**/*.css.map'
            exclude '**/*.js.map'
            exclude '**/test/**'
        }
        from('src/webapp') {
            include 'assets/**'
        }
    }
}
