// --- Rapid Application Development ------------------------------------------------------------------------------------------

task startMariadb {
    description 'Starts MariaDB 10 in a Docker container, on port 3306'
    group 'Backend Development'

    outputs.upToDateWhen {
        "docker ps".execute().in.text.contains("codekvast_database")
    }

    doLast {
        def created = "docker ps -a".execute().in.text.contains("codekvast_database")
        if (created) {
            def command = "docker start codekvast_database"
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0
        } else {
            def command = 'docker pull mariadb:10'
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0

            command = 'docker run -d --restart=unless-stopped ' +
                    '--name codekvast_database ' +
                    '-p 3306:3306 ' +
                    '-e MYSQL_ROOT_PASSWORD=root ' +
                    '-e MYSQL_DATABASE=codekvast ' +
                    '-e MYSQL_USER=codekvast ' +
                    '-e MYSQL_PASSWORD=codekvast ' +
                    '-e TERM=xterm-256color ' +
                    'mariadb:10 ' +
                    '--character-set-server=utf8 ' +
                    '--collation-server=utf8_general_ci ' +
                    '--default-storage-engine=innodb '
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0
        }
    }
}

flyway {
    url = 'jdbc:mariadb://localhost/codekvast'
    user = 'codekvast'
    password = 'codekvast'
    validateOnMigrate = false
}

//--- Run & bootRun -----------------------------------------------------------------------------------------------------
run {
    group "Backend Development"
    dependsOn startMariadb, ":product:warehouse:frontendBuild"

    jvmArgs = ['-ea']
    args = ["--logging.file=$buildDir/log/${applicationName}.log",
            '--codekvast.queuePathPollIntervalSeconds=10',
            '--codekvast.herokuCodekvastUrl=http://localhost:8080',
            // '--codekvast.webappJwtSecret=secret',
            '--codekvast.webappJwtExpirationSeconds=15',
            '--spring.resources.staticLocations=' +
                "file:$projectDir/src/webapp/dist/," +
                'classpath:/META-INF/resources']
}

bootRun {
    group "Backend Development"
    dependsOn startMariadb

    jvmArgs = run.jvmArgs
    args = run.args
    addResources = true
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Warehouse',
            'initInfoDescription'     : 'Codekvast Warehouse aggregates data from many Codekvast Daemons',
    ]
    excludeDevtools = true
}

bootRepackage {
    // Set a classifier, or else gradle distZip will overwrite the repackaged jar...
    classifier = 'all'
    dependsOn ":product:warehouse:frontendBuild"
}
