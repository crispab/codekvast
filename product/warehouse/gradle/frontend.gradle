//--- Frontend -----------------------------------------------------------------------------------

task frontendInstall(type: Exec) {
    description 'Installs the node modules and tools required for frontend development'
    group 'Frontend Development'

    workingDir 'src/webapp'
    executable 'npm'
    args 'install'

    inputs.file file('src/webapp/package.json')
    inputs.file file('src/webapp/typings.json')
    outputs.dir fileTree('src/webapp/node_modules')
    outputs.dir fileTree('src/webapp/typings')
}

task frontendTest(type: Exec) {
    description "Runs Typescript unit tests with Karma and Jasmine"
    group "Frontend Development"

    dependsOn frontendInstall
    mustRunAfter test

    def timestampFile = file("$buildDir/frontendTest.timestamp")

    inputs.file 'src/webapp/package.json'
    inputs.file 'src/webapp/tsconfig.json'
    inputs.file 'src/webapp/webpack.config.json'
    inputs.files fileTree('src/webapp/config')
    inputs.files fileTree('src/webapp/src')
    outputs.file timestampFile

    workingDir 'src/webapp'
    executable 'npm'
    args 'run', 'test'

    doLast {
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn frontendTest


task frontendStart(type: Exec) {
    description 'Starts the webpack dev server on port 8081'
    group 'Frontend Development'

    dependsOn frontendInstall

    workingDir 'src/webapp'
    executable 'npm'
    args 'start'
}

task frontendBuild(type: Exec) {
    description "Builds the frontend webpack bundles for production"
    group "Frontend Development"
    dependsOn frontendTest

    inputs.file file('src/webapp/package.json')
    inputs.file file('src/webapp/tsconfig.json')
    inputs.file file('src/webapp/webpack.config.json')
    inputs.files fileTree('src/webapp/config')
    inputs.files fileTree('src/webapp/src')

    outputs.files fileTree('src/webapp/dist')

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'build'
    environment['CODEKVAST_VERSION']=codekvastDisplayVersion
}

task frontendClean(type: Exec) {
    description "Cleans the frontend webpack bundles"
    group "Frontend Development"

    workingDir file('src/webapp')
    executable 'npm'
    args 'run', 'clean'
}

clean.dependsOn frontendClean

jar {
    dependsOn frontendBuild

    // Spring Boot automatically serves static content from static/**

    // Output from frontendBuild
    into('static') {
        from('src/webapp/dist') {
            exclude '**/*.css.map'
            exclude '**/*.js.map'
        }
    }
}
