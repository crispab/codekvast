//--- Docker ---------------------------------------------------------------------------------------

ext.warehouseImageName = 'crisp/codekvast-warehouse'

task pullLatestDockerImage(type: Exec) {
    description "Pulls the latest $warehouseImageName from Docker Hub"
    group 'Distribution'
    commandLine 'docker', 'pull', "$warehouseImageName:latest"
    ignoreExitValue true
}

distDocker {
    description "Builds the Docker image $warehouseImageName:latest"
    group 'Distribution'
    baseImage 'java:8-jre'
    maintainer 'Olle Hallin "olle.hallin@crisp.se"'
    runCommand 'mkdir -p /var/log /tmp/codekvast'
    volume '/var/log'
    volume '/tmp/codekvast'
    tagVersion 'latest'

    dependsOn check, pullLatestDockerImage
}

task tagDockerImageWithLatest(type: Exec) {
    description 'Assigns version tags the latest Docker image'
    group 'Distribution'
    commandLine "$projectDir/gradle/tagDockerImage.sh", warehouseImageName, codekvastVersion, gitId
}

task pushDockerImage(type: Exec) {
    description 'Pushes the codekvast-warehouse Docker image to Docker hub'
    group 'Distribution'
    commandLine "$projectDir/gradle/pushToDockerHub.sh", warehouseImageName, codekvastVersion, gitId
    dependsOn tagDockerImageWithLatest
}

