// --- End-to-end function test -----------------------------------------------------------------------------------

testSets {
    functionTest
}

functionTest.dependsOn test, testTypeScript
check.dependsOn functionTest

dependencies {
    functionTestCompileOnly lombok

    functionTestCompile project(':product:support:testsupport')
    functionTestCompile 'net.serenity-bdd:serenity-core:1.1.31'
    functionTestCompile 'net.serenity-bdd:serenity-junit:1.1.31'

    functionTestRuntime 'com.codeborne:phantomjsdriver:1.2.1'
}

task startFunctionTestEnvironment(type: Exec) {
    description 'docker-compose up'
    group 'Function test'
    dependsOn ':product:warehouse:distDocker'
    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'up', '-d'
}

task stopFunctionTestEnvironment(type: Exec) {
    description 'docker-compose stop'
    group 'Function test'
    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'stop'
}

task downFunctionTestEnvironment(type: Exec) {
    description 'docker-compose down'
    group 'Function test'
    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'down'
}

task psFunctionTestEnvironment(type: Exec) {
    description 'docker-compose ps'
    group 'Function test'
    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'ps'
}

task logsFunctionTestEnvironment(type: Exec) {
    description 'docker-compose logs'
    group 'Function test'
    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'logs'
}

task endpointForFunctionTestWarehouse(type: Exec) {
    description 'docker-compose port'
    group 'Function test'
    dependsOn startFunctionTestEnvironment

    commandLine 'docker-compose',
            '-p', 'warehouse-functest',
            '-f', 'src/functionTest/resources/docker-compose.yml',
            'port',
            'app',
            '8080'

    standardOutput = new ByteArrayOutputStream()

    ext.endpoint = {
        return standardOutput.toString().trim()
    }
}

functionTest {
    description 'Runs function tests against Codekvast Warehouse in a Docker container'
    group 'Function test'
    dependsOn test, endpointForFunctionTestWarehouse
    finalizedBy stopFunctionTestEnvironment
    shouldRunAfter integrationTest

    doFirst {
        def endpoint = endpointForFunctionTestWarehouse.endpoint()
        waitForPortOpenAt endpoint

        systemProperty 'expectedCodekvastVersion', codekvastDisplayVersion
        systemProperty 'webdriver.base.url', "http://$endpoint"

        // Make Serenity follow Gradle instead of Maven conventions
        systemProperty 'properties', file('src/functionTest/resources/serenity.properties').absolutePath
        systemProperty 'serenity.outputDirectory', file('build/functionTest-serenity').absolutePath
    }
}

def waitForPortOpenAt(String endpoint) {
    String[] parts = endpoint.split(':')
    Socket socket = new Socket()
    socket.connect(new InetSocketAddress(parts[0], Integer.parseInt(parts[1])), 15_000);
    socket.close()
}

check.dependsOn functionTest
