// --- Rapid Application Development ------------------------------------------------------------------------------------------

configurations {
    springLoaded
}

dependencies {
    springLoaded springLoadedAgent
}

task startMariadb {
    description 'Starts MariaDB 10 in a Docker container, on port 3306'
    group 'Backend Development'

    outputs.upToDateWhen {
        "docker ps".execute().in.text.contains("codekvast_database")
    }

    doLast {
        def created = "docker ps -a".execute().in.text.contains("codekvast_database")
        if (created) {
            def command = "docker start codekvast_database"
            logger.lifecycle(command)
            command.execute().waitFor()
        } else {
            def command = 'docker pull mariadb:10'
            logger.lifecycle(command)
            command.execute().waitFor()

            command = 'docker run -d --restart=unless-stopped ' +
                    '--name codekvast_database ' +
                    '-p 3306:3306 ' +
                    '-e MYSQL_ROOT_PASSWORD=root ' +
                    '-e MYSQL_DATABASE=codekvast_warehouse ' +
                    '-e MYSQL_USER=codekvast ' +
                    '-e MYSQL_PASSWORD=codekvast ' +
                    '-e TERM=xterm-256color ' +
                    'mariadb:10 ' +
                    '--character-set-server=utf8 ' +
                    '--collation-server=utf8_general_ci ' +
                    '--default-storage-engine=innodb '
            logger.lifecycle(command)
            command.execute().waitFor()
        }
    }
}

flyway {
    url = 'jdbc:mariadb://localhost/codekvast_warehouse'
    user = 'codekvast'
    password = 'codekvast'
    validateOnMigrate = false
}

//--- Run & bootRun -----------------------------------------------------------------------------------------------------
run {
    group "Backend Development"
    dependsOn startMariadb

    jvmArgs = ['-ea']

    args = [
            "--logging.file=$buildDir/log/${applicationName}.log",
            '--codekvast.importPathPollIntervalSeconds=10',
    ]
}

bootRun {
    group "Backend Development"
    dependsOn startMariadb

    jvmArgs = [
            "-javaagent:${configurations.springLoaded.asPath}",
            '-noverify',
            '-Dspring.messages.cacheSeconds=1'
    ] + run.jvmArgs

    args = run.args
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Warehouse',
            'initInfoDescription'     : 'Codekvast Warehouse aggregates data from many Codekvast Daemons',
    ]
    excludeDevtools = true
}

bootRepackage {
    // Set a classifier, or else gradle distZip will overwrite the repackaged jar...
    classifier = 'all'
}
