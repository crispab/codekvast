buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.ajoberstar:grgit:0.3.1"
    }
}
plugins {
    id 'distribution'
    id 'com.jfrog.bintray' version '0.6'
    id 'com.bmuschko.vagrant' version '2.0'
}


import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

version = "0.5"

ext.gitCommit = Grgit.open("$rootDir").log(maxCommits: 1)[0]
ext.gitVersionTokens = [
        'git.id'          : "$gitCommit.id".toString(),
        'git.committer'   : "$gitCommit.committer.name <$gitCommit.committer.email>".toString(),
        'git.shortMessage': "$gitCommit.shortMessage".toString(),
        'git.time'        : "${new SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new Date(gitCommit.time * 1000L))}".toString(),
]

distributions {
    agent {
        baseName = 'codekvast-agent'
        contents {
            from tasks.getByPath(":product:agent:codekvast-agent:installApp")
            from tasks.getByPath(":product:agent:collector:shadowJar"), { into "endorsed" }
        }
    }

    server {
        baseName = 'codekvast-server'
        contents {
            from tasks.getByPath(":product:server:codekvast-server:installApp")
        }
    }

    web {
        baseName = 'codekvast-web'
        contents {
            from tasks.getByPath(":product:web:installApp")
        }
    }
}

task build(dependsOn: [agentDistZip, serverDistZip, webDistZip]) {
    description = "Builds distribution zips for agent, server and web"
    group "build"
}

bintray {
    // These are supposed to be in $HOME/gradle.properties
    user = "$bintrayUser"
    key = "$bintrayKey"

    filesSpec { //When uploading any arbitrary files ('filesSpec' is a standard Gradle CopySpec)
        from tasks.getByPath(":product:agentDistZip")
        into "codekvast/agent/$version"
    }

    dryRun = false
    publish = true

    pkg {
        repo = 'codekvast'
        userOrg = 'crisp'
        name = 'agent'
        desc = 'Codekvast is a Runtime Intelligence tool used for identifying Truly Dead Code, i.e., code that no-one has accessed in a ' +
                'certain period of time'
        websiteUrl = 'http://codekvast.crisp.se'
        licenses = ['MIT']
        labels = ['runtime-intelligence', 'dead-code', 'dead-code-detector', 'truly-dead-code', 'YANIA', 'java', 'java-6', 'jvm']

        publicDownloadNumbers = false
    }
}
