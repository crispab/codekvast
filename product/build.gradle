buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}
plugins {
    id 'distribution'
    id 'com.jfrog.bintray' version '0.6'
    id 'com.bmuschko.vagrant' version '2.0'
    id 'org.hidetake.ssh' version '0.4.5'
    id "org.ajoberstar.grgit-release" version "0.10.0"
}


import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

version = "0.7"

ext.gitCommit = Grgit.open("$rootDir").log(maxCommits: 1)[0]
ext.gitVersionTokens = [
        'git.id'          : "$gitCommit.id".toString(),
        'git.committer'   : "$gitCommit.committer.name <$gitCommit.committer.email>".toString(),
        'git.shortMessage': "$gitCommit.shortMessage".toString(),
        'git.time'        : "${new SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new Date(gitCommit.time * 1000L))}".toString(),
]

distributions {
    agent {
        baseName = 'codekvast-agent'
        contents {
            from tasks.getByPath(":product:agent:codekvast-agent:installApp")
            from tasks.getByPath(":product:agent:collector:javaagents"), { into "javaagents" }
            from tasks.getByPath(":product:agent:codekvast-agent:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:codekvast-agent:sourcesJar"), { into "src" }
            from tasks.getByPath(":product:agent:collector:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:collector:sourcesJar"), { into "src" }
            from tasks.getByPath(":product:agent:util:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:util:sourcesJar"), { into "src" }
        }
    }

    server {
        baseName = 'codekvast-server'
        contents {
            from tasks.getByPath(":product:server:codekvast-server:installApp")
        }
    }

    web {
        baseName = 'codekvast-web'
        contents {
            from tasks.getByPath(":product:web:installApp")
        }
    }
}

task build(dependsOn: [agentDistZip, serverDistZip, webDistZip]) {
    description = "Builds distribution zips for agent, server and web"
    group "build"
}

bintray {
    // These are supposed to be defined in $HOME/.gradle/gradle.properties
    user = "$bintrayUser"
    key = "$bintrayKey"

    filesSpec {
        from tasks.getByPath(":product:agentDistZip")
        into "agent"
    }

    dryRun = false
    publish = true

    pkg {
        repo = 'codekvast'
        userOrg = 'crisp'
        name = 'agent'
        desc = 'Codekvast is a Runtime Intelligence tool used for identifying Truly Dead Code, i.e., code that no-one has accessed in a ' +
                'certain period of time'
        websiteUrl = 'http://codekvast.crisp.se'
        licenses = ['MIT']
        labels = ['runtime-intelligence', 'dead-code', 'dead-code-detector', 'truly-dead-code', 'YANIA', 'java', 'java-6', 'jvm']

        publicDownloadNumbers = false
    }
}

ssh.settings {
    knownHosts = allowAnyHosts    // Disable host key verification
    pty = true
}

remotes {
    web {
        role 'all'
        host = 'codekvast.crisp.se'
        user = 'ubuntu'
        identity = new File(System.properties['user.home'] + '/.ssh', 'Codekvast.pem')
    }
}

task prepareTargetServers << {
    def etcDir = file('src/provision/etc/')

    ssh.run {
        session(remotes.role('all')) {
            put "$etcDir/redirectPort80", "."

            execute """
sudo apt-get -q update;
sudo apt-get -q upgrade -y;
sudo apt-get install -y unzip;
sudo apt-get install -y openjdk-7-jdk;

sudo echo 'Europe/Stockholm' > /etc/timezone

sudo mv -f redirectPort80 /etc/init.d;
cd /etc/init.d;
chmod +x redirectPort80
sudo update-rc.d redirectPort80 defaults
"""
        }
    }
}

task installWeb {
    group "Provisioning"
    description "Installs Codekvast-web for the first time"
    dependsOn webDistZip

    def zip = file("$buildDir/distributions/codekvast-web-${version}.zip")
    def confDir = file('src/provision/web/conf/')
    def etcDir = file('src/provision/web/etc/')
    def deployStatusFile = file("$buildDir/tmp/deploy-web-${version}.status")

    inputs.file zip
    inputs.dir file('src/provision/web')
    outputs.file deployStatusFile

    doLast {

        println "Deploying $zip ..."

        ssh.run {
            session(remotes.web) {
                put zip, "."
                execute "unzip -o ${zip.name}"

                put confDir, "codekvast-web-${version}"
                put "$etcDir/codekvast-web.sh", "."

                execute """
pkill java
rm -f codekvast-web
ln -s codekvast-web-${version} codekvast-web
sudo mv ./codekvast-web.sh /etc/init.d/codekvast-web
cd /etc/init.d
sudo update-rc.d codekvast-web defaults
sudo chmod +x codekvast-web
/etc/init.d/codekvast-web start
"""
            }
        }
        deployStatusFile.parentFile.mkdirs()
        deployStatusFile.text = "$zip deployed at ${new Date()}\n"
    }
}

task deployWeb {
    group "Provisioning"
    description "Redeploys Codekvast-web with same version and configuration"
    dependsOn webDistZip

    def zip = file("$buildDir/distributions/codekvast-web-${version}.zip")
    def deployStatusFile = file("$buildDir/tmp/deploy-web-${version}.status")

    inputs.file zip
    outputs.file deployStatusFile

    doFirst {
        if (!deployStatusFile.exists()) {
            throw new IllegalStateException("Cannot deployWeb $version before installWeb")
        }
    }

    doLast {

        println "Redeploying $zip ..."
        ssh.run {
            session(remotes.web) {
                put zip, "."
                execute "unzip -o ${zip.name}"

                execute '/etc/init.d/codekvast-web restart'
            }
        }
        deployStatusFile.parentFile.mkdirs()
        deployStatusFile.text = "$zip redeployed at ${new Date()}\n"
    }
}

task getPeople << {
    ssh.run {
        session(remotes.web) {
            get 'people.csv', buildDir
        }
    }
}
