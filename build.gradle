buildscript {
    apply from: "$rootDir/gradle/buildscript.gradle", to: buildscript
}

import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

apply plugin: 'idea'
idea.project.vcs = 'Git'

subprojects {
    buildscript { repositories { jcenter() } }

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    def gitHead = Grgit.open(dir: rootDir).head()

    ext.gitId = "$gitHead.id"[0..6].toString()
    ext.gitIdFull = gitHead.id.toString()
    ext.gitCommitter = "$gitHead.committer.name <$gitHead.committer.email>".toString()
    ext.gitShortMessage = "$gitHead.shortMessage".toString()
    ext.gitTime = new Date(gitHead.time * 1000L).format('yyyy-MM-dd HH:mm:ss')
    ext.gitTimeIso = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date(gitHead.time * 1000L))
    ext.codekvastDisplayVersion = "${codekvastVersion}-${gitId}".toString()

    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

