buildscript {
    apply from: "$rootDir/gradle/buildscript.gradle", to: buildscript
}
import org.ajoberstar.grgit.Grgit

wrapper {
    gradleVersion = '2.10-rc-1'
}

apply plugin: 'idea'

subprojects {
    buildscript { repositories { jcenter() } }

    apply from: "$rootDir/gradle/libs.gradle"

    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

    ext.gitHead = Grgit.open("$rootDir").head()

    ext.gitVersionTokens = [
            'git.id'          : "$gitHead.id"[0..6].toString(),
            'git.committer'   : "$gitHead.committer.name <$gitHead.committer.email>".toString(),
            'git.shortMessage': "$gitHead.shortMessage".toString(),
            'git.time'        : new Date(gitHead.time * 1000L).format('yyyy-MM-dd HH:mm:ss'),
    ]
    ext.gitId = "$gitHead.id"[0..6].toString()
    ext.codekvastDisplayVersion = "${codekvastVersion}-${gitId}".toString()

    repositories { jcenter() }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

