buildscript {
    apply from: "$rootDir/gradle/buildscript.gradle", to: buildscript
}
import org.ajoberstar.grgit.Grgit

wrapper {
    gradleVersion = '2.11'
}

apply plugin: 'idea'
idea.project.vcs = 'Git'

subprojects {
    buildscript { repositories { jcenter() } }

    apply from: "$rootDir/gradle/libs.gradle"

    apply plugin: 'idea'
    idea {
        // Make spring loaded work when doing 'gradle bootRun'...
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

    ext.gitHead = Grgit.open(dir: rootDir).head()

    ext.gitVersionTokens = [
            'git.id'          : "$gitHead.id"[0..6].toString(),
            'git.committer'   : "$gitHead.committer.name <$gitHead.committer.email>".toString(),
            'git.shortMessage': "$gitHead.shortMessage".toString(),
            'git.time'        : new Date(gitHead.time * 1000L).format('yyyy-MM-dd HH:mm:ss'),
    ]

    // For easy consumption in processResources
    ext.gitId = gitVersionTokens['git.id']
    ext.gitCommitter = gitVersionTokens['git.committer']
    ext.gitShortMessage = gitVersionTokens['git.shortMessage']
    ext.gitTime = gitVersionTokens['git.time']

    ext.codekvastDisplayVersion = "${codekvastVersion}-${gitId}".toString()

    repositories { jcenter() }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

