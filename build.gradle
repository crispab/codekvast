buildscript {
    apply from: "$rootDir/gradle/buildscript.gradle", to: buildscript
}

import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

wrapper {
    gradleVersion = '4.8.1'
    distributionType = 'all'
}

apply plugin: 'idea'
idea.project.vcs = 'Git'

subprojects {
    buildscript { repositories { jcenter() } }

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext.gitHead = Grgit.open(dir: rootDir).head()

    ext.gitVersionTokens = [
            'git.id'          : "$gitHead.id"[0..6].toString(),
            'git.committer'   : "$gitHead.committer.name <$gitHead.committer.email>".toString(),
            'git.shortMessage': "$gitHead.shortMessage".toString(),
            'git.time'        : new Date(gitHead.time * 1000L).format('yyyy-MM-dd HH:mm:ss'),
    ]

    // For easy consumption in processResources
    ext.gitId = gitVersionTokens['git.id']
    ext.gitIdFull = gitHead.id.toString()
    ext.gitCommitter = gitVersionTokens['git.committer']
    ext.gitShortMessage = gitVersionTokens['git.shortMessage']
    ext.gitTime = gitVersionTokens['git.time']
    ext.gitTimeIso = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date(gitHead.time * 1000L))
    ext.codekvastDisplayVersion = "${codekvastVersion}-${gitId}".toString()

    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

