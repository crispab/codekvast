plugins {
    id 'java-base'
}
apply from: "$rootDir/gradle/libs.gradle"

description = "Downloads certain tools for easier IDE setup"
configurations { tools }

dependencies {
    tools mariadbDriver
}

task downloadTools(type: Sync) {
    description = "Downloads certain tools so that IDE:s can find them in a well-known location"
    group "IDE support"

    from configurations.tools
    into file("downloaded")
}

build.dependsOn downloadTools

ext.geckoDriver = buildGeckoDriverName()
ext.chromeDriver = buildChromeDriverName()

private String getOsName() {
    System.properties['os.name'] == 'Mac OS X' ? 'macos-' : 'linux-'
}

private GString buildGeckoDriverName() {
    "geckodriver-0.19.1-${getOsName()}${System.properties['os.arch']}.bin"
}

private GString buildChromeDriverName() {
    "chromedriver-2.35-${getOsName()}${System.properties['os.arch']}.bin"
}

task downloadChromeDriver {
    description = "Downloads $chromeDriver from downloads.codekvast.io to be used by web driver tests in other modules"
    group "Test support"

    def downloadDir = file("$rootDir/.tmp/download")
    def downloadUrl = "https://downloads.codekvast.io/${chromeDriver}"
    def destFile = file("$downloadDir/$chromeDriver")

    inputs.property "downloadUrl", downloadUrl
    outputs.file destFile

    doLast {
        downloadDir.mkdirs()

        logger.lifecycle "Downloading $downloadUrl ..."
        ant.get(
            src: downloadUrl,
            dest: destFile,
            usetimestamp: true,
            verbose: true
        )
        "chmod +x $destFile".execute().waitFor()
    }
}
build.dependsOn downloadChromeDriver

task downloadGeckoDriver {
    description = "Downloads $geckoDriver from downloads.codekvast.io to be used by web driver tests in other modules"
    group "Test support"

    def downloadDir = file("$rootDir/.tmp/download")
    def downloadUrl = "https://downloads.codekvast.io/${geckoDriver}"
    def destFile = file("$downloadDir/$geckoDriver")

    inputs.property "downloadUrl", downloadUrl
    outputs.file destFile

    doLast {
        downloadDir.mkdirs()

        logger.lifecycle "Downloading $downloadUrl ..."
        ant.get(
            src: downloadUrl,
            dest: destFile,
            usetimestamp: true,
            verbose: true
        )

        "chmod +x $destFile".execute().waitFor()
    }
}
build.dependsOn downloadGeckoDriver
