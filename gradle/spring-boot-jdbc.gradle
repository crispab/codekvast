//-----------------------------------------------------------------------------------------------------------------------------
// Common stuff for any Spring Boot app that uses JDBC
//-----------------------------------------------------------------------------------------------------------------------------
apply plugin: 'org.flywaydb.flyway'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation "org.flywaydb:flyway-core:$flywayVersion"

    runtimeOnly 'com.zaxxer:HikariCP'
    runtimeOnly mariadbDriver
}

task startMariadb {
    description 'Starts MariaDB 10 in a Docker container, on port 3306'

    outputs.upToDateWhen {
        "docker ps".execute().in.text.contains("codekvast_database")
    }

    doLast {
        def created = "docker ps -a".execute().in.text.contains("codekvast_database")
        if (created) {
            def command = "docker start codekvast_database"
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0
        } else {
            def command = 'docker pull mariadb:10.0'
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0

            def mysql_datadir = "${System.getenv('HOME')}/.codekvast_database"
            command = "mkdir -p $mysql_datadir"
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0

            command = 'docker run -d --restart=unless-stopped ' +
                '--name codekvast_database ' +
                '-p 3306:3306 ' +
                "-v $mysql_datadir:/var/lib/mysql " +
                '-e MYSQL_ROOT_PASSWORD=root ' +
                '-e MYSQL_DATABASE=codekvast ' +
                '-e MYSQL_USER=codekvast ' +
                '-e MYSQL_PASSWORD=codekvast ' +
                '-e TERM=xterm-256color ' +
                'mariadb:10.0 ' +
                '--character-set-server=utf8 ' +
                '--collation-server=utf8_general_ci ' +
                '--default-storage-engine=innodb '
            logger.lifecycle(command)
            assert command.execute().waitFor() == 0
        }
    }
}

flyway {
    url = 'jdbc:mariadb://localhost/codekvast'
    user = 'codekvast'
    password = 'codekvast'
    validateOnMigrate = false
    table = 'schema_version'
    locations = "filesystem:$rootDir/product/db-migration/src/main/resources/db/migration/"
}

