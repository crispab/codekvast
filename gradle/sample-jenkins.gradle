apply plugin: 'java'
apply from: "$rootDir/gradle/libs.gradle"

configurations {
    tomcatEndorsed
}

dependencies {
    tomcatEndorsed project(path: ":product:java-agent", configuration: 'shadow')
    tomcatEndorsed aspectjWeaver
    tomcatEndorsed "org.slf4j:slf4j-jdk14:1.7.7"
}

defaultTasks 'run'

ext.tomcatDownloadUrl = "https://dl.bintray.com/crisp/codekvast/apache-tomcat-${tomcatVersion}.zip"
ext.tomcatDir = file("$buildDir/tomcat")
ext.catalinaBase = new File(tomcatDir, "apache-tomcat-${tomcatVersion}")
ext.catalinaEndorsed = new File(catalinaBase, "endorsed")
ext.catalinaWebapps = new File(catalinaBase, "webapps")

ext.jenkinsDownloadUrl = "https://dl.bintray.com/crisp/codekvast/jenkins-${jenkinsVersion}.war"

task installJenkinsInTomcat7 {
    def downloadDir = file("$rootDir/.tmp/download")
    def jenkinsDir = new File(catalinaWebapps, "jenkins")

    dependsOn ':product:java-agent:shadowJar'

    inputs.property "tomcatVersion", tomcatVersion
    inputs.property "tomcat", tomcatDownloadUrl
    inputs.property "jenkinsVersion", jenkinsVersion
    inputs.property "jenkins", jenkinsDownloadUrl
    inputs.file file("build.gradle")
    outputs.dir tomcatDir

    doLast {
        downloadDir.mkdirs()
        catalinaWebapps.mkdirs()

        logger.lifecycle "Downloading $tomcatDownloadUrl ..."
        ant.get(
                src: tomcatDownloadUrl,
                dest: downloadDir,
                usetimestamp: true,
                verbose: true
        )

        logger.lifecycle "Unpacking Tomcat into $tomcatDir ..."
        copy {
            from zipTree("$downloadDir/apache-tomcat-${tomcatVersion}.zip")
            into tomcatDir
        }
        exec { commandLine "chmod", "+x", "$catalinaBase/bin/catalina.sh" }

        logger.lifecycle "Setting HTTP port $tomcatPort..."
        def serverXml = file("$catalinaBase/conf/server.xml")
        serverXml.text = serverXml.text.replace("8080", tomcatPort).replace("8005", "0").replace("8009", "0")

        logger.lifecycle "Downloading $jenkinsDownloadUrl into $downloadDir ..."
        ant.get(
                src: jenkinsDownloadUrl,
                dest: downloadDir,
                usetimestamp: true,
                verbose: true
        )

        logger.lifecycle "Copying jenkins.war into $catalinaWebapps ..."
        copy {
            from "$downloadDir/jenkins-${jenkinsVersion}.war"
            into catalinaWebapps
            rename { "jenkins.war" }
        }

        if (jenkinsDir.exists()) {
            logger.lifecycle "Deleting old exploded jenkins dir..."
            delete jenkinsDir
        }
        logger.lifecycle "Jenkins downloaded and deployed to Tomcat."
    }
}

task installTomcatEndorsed(type: Sync) {
    from configurations.tomcatEndorsed
    into catalinaEndorsed
}

task cleanCatalinaEndorsed {
    doLast {
        delete catalinaEndorsed
    }
}

task generateCodekvastConfig {
    group "Build"
    description "Generates a codekvast.conf for test"
    dependsOn ":product:java-agent:assemble"

    ext.configFile = file("$buildDir/codekvast.conf")

    inputs.file file("$rootDir/product/build.gradle")
    inputs.file file("build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Codekvast against Jenkins in Tomcat
#aspectjOptions = -verbose -showWeaveInfo
appVersion = manifest jenkins-cli.*\\\\.jar Jenkins-CLI-Version
aspectjOptions = -verbose
codeBase = $catalinaBase/webapps/jenkins, ${System.properties['user.home']}/.jenkins/plugins
methodVisibility = private
packages = hudson, jenkins
tags = development, sample
verbose = true
"""
    }
}

processResources.dependsOn generateCodekvastConfig

task generateTomcatSetenvSh {
    description = "Generates a setenv.sh in \$CATALINA_HOME/bin that hooks up Codekvast"
    group = "build"
    dependsOn installJenkinsInTomcat7, generateCodekvastConfig, installTomcatEndorsed
    def setenvFile = file("$catalinaBase/bin/setenv.sh")

    inputs.file file("build.gradle")
    outputs.file setenvFile

    doLast {
        def codekvastCollector = file(catalinaEndorsed).listFiles().find { it.name.contains('codekvast') }
        def aspectjWeaver = file(catalinaEndorsed).listFiles().find { it.name.contains('aspectjweaver') }
        def loggingProperties = file("$buildDir/logging.properties")
        def logFile = file("$buildDir/log/tomcat.log")
        logFile.parentFile.mkdirs()

        if (!loggingProperties.exists()) {
            logger.lifecycle "Creating $loggingProperties ..."
            loggingProperties.text = """
handlers = java.util.logging.FileHandler, java.util.logging.ConsoleHandler

java.util.logging.FileHandler.formatter = java.util.logging.SimpleFormatter
java.util.logging.FileHandler.level = ALL
java.util.logging.FileHandler.pattern = $logFile
java.util.logging.FileHandler.limit = 10000000

java.util.logging.ConsoleHandler.formatter = java.util.logging.SimpleFormatter
java.util.logging.ConsoleHandler.level = ALL

java.util.logging.SimpleFormatter.format = %1\$tF %1\$tT [%4\$s] %3\$s - %5\$s%6\$s%n

io.codekvast.aspectjweaver.level = INFO
io.codekvast.javaagent.level = FINE
"""
        }

        println """---------------------------
codekvastCollector = .${codekvastCollector.absolutePath - projectDir.absolutePath}
aspectjWeaver = .${aspectjWeaver.absolutePath - projectDir.absolutePath}
-------------------------"""
        setenvFile.text = """
CATALINA_OPTS=\"-Dcodekvast.configuration=$generateCodekvastConfig.configFile -javaagent:$codekvastCollector -javaagent:$aspectjWeaver\"
LOGGING_CONFIG=\"-Djava.util.logging.config.file=$loggingProperties\"
LOGGING_MANAGER=\"-Djava.util.logging.manager=java.util.logging.LogManager\"
"""
    }
}

task run(type: Exec) {
    description = "Run Jenkins with Codekvast Collector attached"
    group = "application"
    dependsOn generateTomcatSetenvSh
    commandLine "$catalinaBase/bin/catalina.sh", "run"

    // Illustrate that config can be injected from environment variables
    environment['CODEKVAST_APP_NAME']='Jenkins'
}

