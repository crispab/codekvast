apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.7
version = duckVersion

mainClassName = "se.crisp.duck.agent.service.Agent"

dependencies {
    compile libs['aspectjweaver']
    compile libs['logback']
    compile libs['reflections']
    compile libs['slf4j-api']
    compile project(":agent:util")

    testCompile libs['junit']
    testCompile libs['mockito']
}

jar {
    exclude "lombok"
}

test.dependsOn ":sample:standalone-app:jar"

// We sensor the Sample App by default...
run {
    dependsOn = [":sample:standalone-app:installApp", ":sample:standalone-app:generateDuckConfig"]
    args tasks.getByPath(":sample:standalone-app:generateDuckConfig").configFile
}

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "logback.xml"
    rename { "${it}.sample" }
}

startScripts {
    doLast {
        outputs.files.each { script ->
            logger.info "Adjusting CLASSPATH in {} to start with \$APP_HOME/conf/ ...", script.name
            def text = script.text
            text = text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
            script.text = text
        }
    }
}
