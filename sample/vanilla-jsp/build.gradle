apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = 1.5
archivesBaseName = "sample-jsp"

ext.duckDataPath = "/tmp/duck/agent/Crisp/" + archivesBaseName

task generateDuckConfig {
    group = "Build"
    description = "Generates a duck.properties for test"

    ext.configFile = file("build/libs/duck.properties")

    inputs.file file("$projectDir/build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Duck against Crisp Sample Vanilla JSP app
customerName = Crisp
appName = $archivesBaseName
environment = development
codeBaseUri = file:/maven/${archivesBaseName}.war
dataPath = $duckDataPath
packagePrefix = sample
serverUri = http://localhost:8080
sensorDumpIntervalSeconds = 10
serverUploadIntervalMillis = 5000
aspectjOptions = -verbose -showWeaveInfo
verbose = true
"""
    }
}

assemble.dependsOn generateDuckConfig

task dockerRunWarInTomcat7(type: Exec) {
    def tomcatVersion = "tomcat-7.0"
    def port = 8080

    description = "Deploys $project.name to $tomcatVersion inside a Docker container, access it at http://localhost:$port/$project.name"
    group = "Web application"
    dependsOn "assemble", ":product:agent:sensor:assemble"

    def duckSensor = new File(tasks.getByPath(":product:agent:sensor:shadowJar").outputs.files.asPath)

    executable = "docker"
    args = ["run", "--rm",
            "-v", "$duckDataPath:$duckDataPath",
            "-v", "$buildDir/libs:/maven",
            "-v", "$duckSensor.parentFile:/duck",
            "-e", "JAVA_OPTS=-javaagent:/duck/$duckSensor.name=/maven/duck.properties",
            "-e", "LANG=C.UTF-8",
            "-p", "$port:8080",
            "jolokia/$tomcatVersion",
    ]

    doFirst {
        new File(duckDataPath).mkdirs()
        logger.lifecycle description
        logger.info "{} {}", executable, args.join(" ")
    }
}
