apply plugin: 'java'
apply plugin: 'application'

mainClassName = "sample.app.SampleApp"
archivesBaseName = "standalone-app"

sourceCompatibility = 1.6

dependencies {
    compile "org.slf4j:slf4j-api:1.7.7"
    runtime "ch.qos.logback:logback-classic:1.1.2"
}

task generateDuckConfig {
    group "Build"
    description "Generates a duck.properties for test"

    ext.configFile = file("build/libs/duck.properties")

    inputs.file file("$projectDir/build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Duck against Crisp Sample App
customerName = Crisp
appName = $archivesBaseName
environment = development
codeBaseUri = file:${file("build/install/${project.name}/lib/")}
packagePrefix = sample
dataPath = ${file("$buildDir/duck")}
serverUri = http://localhost:8180
sensorDumpIntervalSeconds = 10
serverUploadIntervalSeconds = 5
aspectjOptions = -verbose
verbose = true
"""
    }
}

processResources.dependsOn generateDuckConfig

run {
    dependsOn = [":product:agent:sensor:assemble", "generateDuckConfig"]
    def duckSensor = tasks.getByPath(":product:agent:sensor:shadowJar").outputs.files.asPath
    jvmArgs = ["-javaagent:$duckSensor=$generateDuckConfig.configFile"]
}
