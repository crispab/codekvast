//-----------------------------------------------------------------------------------------
// Illustrates how to attach codekvast-javaagent.jar to a repackaged Spring Boot jar
//-----------------------------------------------------------------------------------------
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

version = '1.0.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    codekvastagent
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.16'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile project(":sample:sample-lib")
    testCompile 'org.springframework.boot:spring-boot-starter-test'

    codekvastagent project(path: ":product:java-agent", configuration: 'shadow')
}

build.dependsOn bootRepackage

task generateCodekvastConfig {
    group "Build"
    description "Generates a codekvast.conf for test"

    def configFile = file("$buildDir/codekvast.conf")

    inputs.file file("build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Codekvast against Sample App
appName = $archivesBaseName
appVersion = 1.3
aspectjOptions = -verbose -showWeaveInfo
codeBase = not-used
excludePackages = sample.lib.excluded
methodVisibility = all
packages = sample. , foo.bar.
tags = development, sample
bridgeAspectjMessagesToJUL = true

# serverUrl = https://api-staging.codekvast.io
httpProxyHost =   
httpProxyPort = 4711
"""
    }
}

processResources.dependsOn generateCodekvastConfig

task launchExecutableJar(type: Exec, dependsOn: [bootRepackage, generateCodekvastConfig, ":product:java-agent:shadowJar"] ) {
    description "Launches the executable jar produced by bootRepackage"
    group "Application"

    executable "java"

    // args "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005"

    args "-javaagent:${configurations.codekvastagent.asPath}"
    args "-Xbootclasspath/a:${configurations.codekvastagent.asPath}"
    args "-Dcodekvast.configuration=$generateCodekvastConfig.outputs.files.asPath"

    args "-jar", "build/libs/${archivesBaseName}-${version}.jar"
}
