apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = 1.6
archivesBaseName = "sample-t5"

dependencies {
    compile "org.apache.tapestry:tapestry-core:5.3.6"
    runtime "ch.qos.logback:logback-core:1.1.2"
}

// Get rid of Commons Logging and Log4j...
configurations {
    all*.exclude group: "commons-logging", module: "commons-logging"
    all*.exclude group: "log4j", module: "log4j"
    all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
}

jettyRun {
    reload = "automatic"
    scanIntervalSeconds = 5
    contextPath = ""
}

ext.codekvastDataPath = "/tmp/codekvast/agent/Crisp/" + archivesBaseName

task generateCodeKvastConfig {
    group = "Build"
    description = "Generates a codekvast.properties for test"

    ext.configFile = file("build/libs/codekvast.properties")

    inputs.file file("$projectDir/build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run CodeKvast against Crisp Sample App
customerName = Crisp
appName = $archivesBaseName
appVersion = $version
environment = development
codeBaseUri = file:/maven/${archivesBaseName}.war
dataPath = $codekvastDataPath
packagePrefix = se.crisp.sample
serverUri = http://localhost:8080
apiUsername = agent
apiPassword = 0000
sensorResolutionIntervalSeconds = 10
serverUploadIntervalSeconds = 5
aspectjOptions = -verbose
verbose = true
"""
    }
}

assemble.dependsOn generateCodeKvastConfig

task dockerRunWarInTomcat7(type: Exec) {
    def tomcatVersion = "tomcat-7.0"
    def port = 8080

    description = "Deploys $project.name to $tomcatVersion inside a Docker container, access it at http://localhost:$port/$project.name"
    group = "Web application"
    dependsOn "assemble", ":product:agent:sensor:assemble"

    def codekvastSensor = new File(tasks.getByPath(":product:agent:sensor:shadowJar").outputs.files.asPath)

    executable = "docker"
    args = ["run", "--rm",
            "-v", "$codekvastDataPath:$codekvastDataPath",
            "-v", "$buildDir/libs:/maven",
            "-v", "$codekvastSensor.parentFile:/codekvast",
            "-e", "JAVA_OPTS=-javaagent:/codekvast/$codekvastSensor.name=/maven/codekvast.properties",
            "-e", "LANG=C.UTF-8",
            "-p", "$port:8080",
            "jolokia/$tomcatVersion",
    ]

    doFirst {
        new File(codekvastDataPath).mkdirs()
        logger.lifecycle description
        logger.info "{} {}", executable, args.join(" ")
    }
}
